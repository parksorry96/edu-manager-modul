plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
}

// 공통 설정
allprojects {
	group = 'com.edumanager'
	version = '0.0.1-SNAPSHOT'
	
	repositories {
		mavenCentral()
	}
}

// 서브프로젝트 공통 설정
subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	
	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}
	
	// 공통 의존성
	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}
	
	tasks.named('test') {
		useJUnitPlatform()
	}
	
	// 비실행 가능 jar 생성 (메인 애플리케이션 제외)
	jar {
		enabled = true
		archiveClassifier = ''
	}
	
	bootJar {
		enabled = false
	}
}

// 메인 애플리케이션 모듈만 실행 가능 jar 생성
project(':application:api') {
	bootJar {
		enabled = true
		archiveClassifier = ''
	}
	
	jar {
		enabled = false
	}
}